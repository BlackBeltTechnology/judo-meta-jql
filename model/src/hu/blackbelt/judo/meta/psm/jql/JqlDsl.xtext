grammar hu.blackbelt.judo.meta.psm.jql.JqlDsl hidden(WS, ML_COMMENT, SL_COMMENT)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate jqlDsl "http://www.blackbelt.hu/judo/meta/psm/jql/JqlDsl"

Expression returns Expression:
	ConcatenateExpression;

ConcatenateExpression returns Expression:
	OrExpression (=>({BinaryOperation.leftOperand=current} feature=OpConcat) rightOperand=OrExpression)*;

OpConcat:
	'||';

OrExpression returns Expression:
	AndExpression (=>({BinaryOperation.leftOperand=current} feature=OpOr) rightOperand=AndExpression)*;

OpOr:
	'or' | 'OR';

AndExpression returns Expression:
	EqualityExpression (=>({BinaryOperation.leftOperand=current} feature=OpAnd) rightOperand=EqualityExpression)*;

OpAnd:
	'and' | 'AND';

EqualityExpression returns Expression:
	RelationalExpression (=>({BinaryOperation.leftOperand=current} feature=OpEquality)
	rightOperand=RelationalExpression)*;

OpEquality:
	'==' | '!=' ;

RelationalExpression returns Expression:
	AdditiveExpression
	(=>({BinaryOperation.leftOperand=current} feature=OpCompare) rightOperand=AdditiveExpression)*;

OpCompare:
	'>=' | '<' '=' | '>' | '<' ;

AdditiveExpression returns Expression:
	MultiplicativeExpression (=>({BinaryOperation.leftOperand=current} feature=OpAdd)
	rightOperand=MultiplicativeExpression)*;

OpAdd:
	'+' | '-';

MultiplicativeExpression returns Expression:
	UnaryOperation (=>({BinaryOperation.leftOperand=current} feature=OpMulti) rightOperand=UnaryOperation)*;

OpMulti:
	'*' | '/' | '%';

UnaryOperation returns Expression:
	{UnaryOperation} feature=OpUnary operand=UnaryOperation
	| PrimaryExpression;

OpUnary:
	"!" | 'not' | 'NOT' | "-" | "+";

ParenthesizedExpression returns Expression:
	'(' Expression ')';

PrimaryExpression returns Expression:
	QualifiedName |
	Literal |
	ParenthesizedExpression;

Literal returns Expression:
	BooleanLiteral |
	NumberLiteral |
	StringLiteral;

BooleanLiteral returns Expression :
	{BooleanLiteral} ('false' | isTrue?='true');

NumberLiteral returns Expression :
	{IntegerLiteral} value=INTEGER |
    {DecimalLiteral} value=DECIMAL;

StringLiteral returns Expression:
	{StringLiteral} value=STRING;

QualifiedName:
	base=ValidID (=>'.' navigations+=ValidID)*;

ValidID:
	ID;

terminal DECIMAL returns ecore::EBigDecimal: INTEGER '.' INTEGER;
terminal INTEGER returns ecore::EBigInteger: ('0'..'9')+;

terminal ID: '^'?('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'0'..'9')*;
terminal STRING:
			'"' ( '\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\'|'"') )* '"' |
			"'" ( '\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\'|"'") )* "'"
		;
terminal ML_COMMENT : '/*' -> '*/';
terminal SL_COMMENT : '//' !('\n'|'\r')* ('\r'? '\n')?;

terminal WS         : (' '|'\t'|'\r'|'\n')+;

terminal ANY_OTHER: .;
